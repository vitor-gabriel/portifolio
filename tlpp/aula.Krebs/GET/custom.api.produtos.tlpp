#INCLUDE "TLPP-CORE.TH"
#INCLUDE "TLPP-REST.TH"

NameSpace custom.api
@GET('/api/v2/custom/produtos')
User Function GetProdutos()
	Local jResponse := JsonObject():New()
	Local jQuery := oRest:GetQueryRequest()
	Local cQuery := ""
	Local cFinalQuery := ""
	Local cAlias := ""
	Local aDados := {} 

	cQuery := "SELECT B1_COD, B1_DESC FROM SB1990 WHERE 1=1 AND D_E_L_E_T_ <> '*'"

	If jQuery:HasProperty("b1_cod")
		cQuery += " AND B1_COD IN (?)"
	EndIf

	oQuery := FWPreparedStatement():New()
	oQuery:SetQuery(cQuery)

	If jQuery:HasProperty("b1_cod")
		oQuery:SetIn(1,StrToKarr2(jQuery['b1_cod'],","))
	EndIf

	cFinalQuery := oQuery:GetFixQuery()
	cAlias := MPSysOpenQuery(cFinalQuery)

	If (cAlias)->(!Eof())
		oRest:setStatusCode(200)
		jResponse['status'] := "success"
		While (cAlias)->(!Eof())
			aAdd(aDados,JsonObject():New())
			aDados[Len(aDados)]['b1_cod'] := (cAlias)->B1_COD
			aDados[Len(aDados)]['b1_desc'] := (cAlias)->B1_DESC
			(cAlias)->(DbSkip())
		EndDo
		jResponse['produtos'] := aDados
	Else
		oRest:setStatusCode(400)
		jResponse['status'] := "error"
		jResponse['message'] := "Nenhum registro encontrado"
	EndIf

	oRest:setKeyHeaderResponse('Content-Type','application/json')
	oRest:setResponse(jResponse)

	oQuery:Destroy()
	FwFreeObj(oQuery)
Return

